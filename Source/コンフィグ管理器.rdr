コンフィグ管理器とは
	単一種類
	
	+コンフィグ初期値={
		「Design」={
			「Theme」={
				「初期値」=「Dark」,
				「名前」=「テーマ」,
				「説明」=「[アプリの名前] のウィンドウテーマ[改行](テーマの変更はアプリを再起動すると適用されます。)」,
				「型」=文字列,
				「一覧」=ウィンドウ管理器のテーマ一覧の見出し
			}
		},
		「Display」={
			「Resolution_Width」={
				「初期値」=「1920」,
				「名前」=「ディスプレイの解像度(幅)」,
				「説明」=「通常時のメインディスプレイの解像度(幅)」,
				「型」=整数,
				「最小値」=640,
				「最大値」=5120
			},
			「Resolution_Height」={
				「初期値」=「1080」,
				「名前」=「ディスプレイの解像度(高さ)」,
				「説明」=「通常時のメインディスプレイの解像度(高さ)」,
				「型」=整数,
				「最小値」=640,
				「最大値」=5120
			},
			「RefreshRate」={
				「初期値」=「60」,
				「名前」=「ディスプレイのリフレッシュレート」,
				「説明」=「通常時のメインディスプレイのリフレッシュレート」,
				「型」=整数,
				「最小値」=60,
				「最大値」=360
			}
		},
		「General」={
			「EAapp_Config_File_Name」={
				「初期値」=「」,
				「名前」=「EA app コンフィグファイル」,
				「説明」=「EA app のコンフィグファイルのファイル名[改行]プレイする際のプラットフォームが EA app の場合、この項目は必須です。」,
				「型」=文字列,
				「追加項目」={
					『
					ボタンをベース部品の項目パネルへ作る
						その位置と大きさ=｛744,10,30,30｝
						その内容=「自動検出を実行」
						その見た目=「フラット」
						そのフォント=「Yu Gothic UI,9」
						その背景画像配置=「拡大」
						そのドッキング方向=「上」
						
						そのクリックされた時の手順は、『
						EAapp:コンフィグ取得する
						もしEAappのコンフィグファイルが無でないなら
							メイン設定について「General」で「EAapp_Config_File_Name」へEAappのコンフィグファイルを設定する
							メイン画面のアプリ設定パネルでコンフィグ読み込む
						もし終わり
						』
					』
				}
			}
		},
		「Update」={
			「CheckForUpdatesOnAppLaunch」={
				「初期値」=○,
				「名前」=「アプリの起動時にアップデートを確認する」,
				「説明」=「アプリの起動時にアップデートの確認を行うかどうか」,
				「型」=真偽値
			},
			「ReleaseChannel」={
				「初期値」=「[アプリのリリースチャンネル]」,
				「名前」=「アップデートのリリースチャンネル」,
				「説明」=「アップデート時にインストールするバージョンのリリースチャンネル」,
				「型」=文字列,
				「一覧」={「release」,「beta」,「beta_only」,「dev」}
			}
		},
		「Window_Main」={
			「Profile_Sort_Order」={「初期値」=「1」},
			//「Position_X」=「0」,
			//「Position_Y」=「0」,
			「Size_W」={「初期値」=「900」},
			「Size_H」={「初期値」=「650」}
		},
		「Window_InPlay」={
			//「Position_X」=「0」,
			//「Position_Y」=「0」,
			「Size_W」={「初期値」=「800」},
			「Size_H」={「初期値」=「600」}
		}
	}
	
	+フォルダー一覧:配列={
		「Logs」,
		「Logs\Debug」,
		「Update」,
		「Profile」,
		「Profile\AutoExecute」,
		「Profile\v1」,
		「Resources」,
		「Resources\TitleScreenImage」
	}
	
	+初回起動:真偽値
	はじめの手順
		メイン設定のファイル名=「[アプリのコンフィグフォルダー]Config.ini」
		
		初回起動=×
	終わり
	
	自分でコンフィグチェックする手順
		// 各フォルダーを作成
		もし「[アプリのコンフィグフォルダー]」というフォルダが存在しないなら「[マイドキュメント]」へ「[アプリの名前]」というフォルダを作成する
		フォルダー一覧を【フォルダー名】へそれぞれ繰り返す
			もし「[アプリのコンフィグフォルダー][フォルダー名]」というフォルダが存在しないなら「[マイドキュメント]」へ「[アプリのコンフィグフォルダー][フォルダー名]」というフォルダを作成する
		繰り返し終わり
		
		// 初期プロファイルを作成
		もし「[アプリのコンフィグフォルダー]Profile\Default.json」というファイルが存在しないなら
			【デフォルトプロファイル】=プロファイルを作ったもの
			デフォルトプロファイルのID=「Default」
			デフォルトプロファイルの名前=「デフォルト」
			デフォルトプロファイルを保存する
		もし終わり
		
		// プロファイルを読み込む
		プロファイル管理器でプロファイル読み込む
		
		もしメイン設定のファイル名というファイルが存在しないなら
			初回起動=○
			// インストール感謝しますダイアログを表示する そのうち専用のウィンドウを作ってそれと置き換える
			「[アプリの名前] をインストールしていただき、ありがとうございます！
	
[アプリの名前] はベータ版であり、予期せぬ不具合などが発生する可能性があります。
不具合などの問題を見つけたり、改善してほしい点があった場合は、
サポート Discord サーバーのフォーラム または GitHub の Issue にて報告をお願いいたします。」を「情報アイコン」で表示する
			// セットアップを実行
			//セットアップする
		もし終わり
		
		メイン設定について「Info」で「App_FullVersion」へ「[アプリのバージョン表記]」を設定する
		メイン設定について「Info」で「App_Version」へ「[アプリのバージョン]」を設定する
		メイン設定について「Info」で「App_ReleaseChannel」へ「[アプリのリリースチャンネル]」を設定する
		メイン設定について「Info」で「App_ReleaseNumber」へ「[アプリのリリース番号]」を設定する
		
		// コンフィグの値が存在するかチェックして、存在しなければ追加する
		コンフィグ管理器のコンフィグ初期値の見出しを【コンフィグ親キー】へそれぞれ繰り返す
			コンフィグ管理器のコンフィグ初期値(コンフィグ親キー)の見出しを【コンフィグキー】へそれぞれ繰り返す
				もし[メイン設定についてコンフィグ親キーからコンフィグキーを取得したもの]が空ならメイン設定についてコンフィグ親キーでコンフィグキーへ[コンフィグ管理器のコンフィグ初期値(コンフィグ親キー)(コンフィグキー)(「初期値」)]を設定する
			繰り返し終わり
		繰り返し終わり
		
		// メインディスプレイの解像度を取得してコンフィグに設定する
		10回【カウント】にカウントしながら繰り返す
			【ディスプレイ】=画面のカウント番目を取得したもの
			もしディスプレイのメインが○なら
				メイン設定について「Display」で「Resolution_Width」へディスプレイの幅を設定する
				メイン設定について「Display」で「Resolution_Height」へディスプレイの高さを設定する
				// ウィンドウの位置をディスプレイの中心に設定する
				もしメイン設定について「Window_Main」から「Position_X」を取得したものが無ならメイン設定について「Window_Main」で「Position_X」へ((ディスプレイの幅/2)-コンフィグ初期値(「Window_Main」)(「Size_H」)/2)を設定する
				もしメイン設定について「Window_InPlay」から「Position_X」を取得したものが無ならメイン設定について「Window_InPlay」で「Position_X」へ[メイン設定について「Window_Main」から「Position_X」を取得したもの]を設定する
				もしメイン設定について「Window_Main」から「Position_Y」を取得したものが無ならメイン設定について「Window_Main」で「Position_Y」へ((ディスプレイの高さ/2)-コンフィグ初期値(「Window_Main」)(「Size_W」)/2)を設定する
				もしメイン設定について「Window_InPlay」から「Position_Y」を取得したものが無ならメイン設定について「Window_InPlay」で「Position_Y」へ[メイン設定について「Window_Main」から「Position_Y」を取得したもの]を設定する
				繰り返しから抜け出す
			もし終わり
		繰り返し終わり
		
		// ウィンドウのテーマを設定する
		ウィンドウ管理器のテーマ名=メイン設定について「Design」から「Theme」を取得したもの
		
		【ランチャーインストール済み:真偽値】=×
		ランチャーインストール済み=Steamのインストール済み
		
		// EA app のコンフィグを読み込む
		もしEAappのインストール済みが○なら
			EAappでコンフィグ読み込む
		もし終わり
		もしランチャーインストール済みが×ならランチャーインストール済み=EAappのインストール済み
		
		もしランチャーインストール済みが×なら
			「Steam または EA app のいずれかがインストールされていることを確認できませんでした。[改行]Steam または EA app のいずれかをインストールしているにも関わらず、この警告が表示されている場合は、開発者に報告してください。」を「警告アイコン」で表示する
		もし終わり
	終わり
	
	自分でセットアップする手順
		セットアップ画面を表示する
	終わり
終わり