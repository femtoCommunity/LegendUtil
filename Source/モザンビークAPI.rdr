モザンビークAPIとは
	単一種類
	
	-APIURL:文字列=「https://legendutil_mbapi-1-z1481468.deta.app」
	
	-エラーニュースデータ:配列={
		{
			「title」=「ニュースの取得に失敗しました」,
			「link」=「」,
			「img」=「」,
			「short_desc」=「」
		}
	}
	+ニュースデータ:配列
	-ニュース最終取得日時:日時形式
	
	はじめの手順
		ニュースデータ=初期ニュースデータ
		ニュース最終取得日時=日時形式(「2000/01/01」)を作る
	終わり
	
	自分でニュース更新する手順
		// 10秒間隔を空けないと取得できないようにする
		【経過時間:日時期間形式】=ニュース最終取得日時と今の日付差
		もし経過時間の合計秒数が10未満なら
			「間隔が短すぎます。[改行][10-経過時間の合計秒数の整数]秒後にもう一度お試しください。」というエラーを発生させる
			手順から抜け出す
		もし終わり
		
		【データ】
		例外監視
			// 画像ファイルを削除する
			「[アプリのコンフィグフォルダー]Resources\News\Image\*.jpg」を削除する
			
			データ=「[APIURL]/news?language=ja-jp」をHTTPで取得したもの
			「ニュースデータ取得 - ステータスコード: [HTTPのステータスコード]」をデバッグログへ出力する
			ニュースデータ=データをJSON形式として読み取ったもの
			もしHTTPのステータスコードが200でないなら
				ニュースデータ=エラーニュースデータ
				ニュースデータ(1)(「short_desc」)=「ステータスコード: [HTTPのステータスコード]」
				「[今の位置]Resources\Images\News\Warn.jpg」を「[アプリのコンフィグフォルダー]Resources\News\Image\1.jpg」へコピーする
				「ニュースの取得に失敗しました。[改行]ステータスコード: [HTTPのステータスコード]」というエラーを発生させる
				手順から抜け出す
			もし終わり
			// 画像を取得する
			【カウント:整数】=1
			ニュースデータを【ニュース】へそれぞれ繰り返す
				[ニュース(「img」)]を「[アプリのコンフィグフォルダー]Resources\News\Image\[カウント].jpg」へHTTPでダウンロードする
				カウント=カウント+1
			繰り返し終わり
		発生した場合
			「[エラーのメッセージ]」をデバッグログへ出力する
			ニュースデータ=エラーニュースデータ
			ニュースデータ(1)(「short_desc」)=「[エラーのメッセージ]」
			「[今の位置]Resources\Images\News\Warn.jpg」を「[アプリのコンフィグフォルダー]Resources\News\Image\1.jpg」へコピーする
			手順から抜け出す
		監視終わり
	終わり
終わり