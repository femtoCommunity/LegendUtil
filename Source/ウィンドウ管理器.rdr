ウィンドウ管理器とは
	単一種類
	
	+テーマ背景色
	+テーマ文字色
	+テーマアイコン色
	-テーマ部品固有色
	+部品一覧
	+テーマ一覧
	
	はじめの手順
		テーマ背景色=テーマ背景色という色情報を作ったもの
		テーマ文字色=テーマ文字色という色情報を作ったもの
		テーマアイコン色=RGB(255,255,255)
		テーマ部品固有色=○
		部品一覧=空の辞書
		
		【ダーク色】={255,25,25,25}
		【ライト色】={255,250,250,250}
		
		テーマ一覧={ // 背景,文字,アイコン (ARGB)
			「Dark」={ダーク色,ライト色,ライト色},
			「Light」={ライト色,ダーク色,ダーク色},
			「SkyBlue」={{255,0,140,225},{255,255,255,255},{255,250,250,250}},
			「HyperPop」={{255,224,53,118},{255,255,255,255},{255,250,250,250}},
			「Trick or Treat」={{255,64,0,128},{255,255,128,0},{255,250,250,250}}
		}
		
		テーマ名=テーマ一覧の見出し(1)
		
		初期化状態=空の辞書
	終わり
	
	-テーマ名データ:文字列
	テーマ名という属性
		設定する手順
			テーマ名データ=設定値
			テーマ背景色の要素=テーマ一覧(設定値)(1)
			テーマ文字色の要素=テーマ一覧(設定値)(2)
			テーマアイコン色の要素=テーマ一覧(設定値)(3)
		終わり
		取得する手順
			テーマ名データを返す
		終わり
	終わり
	
	【アイコン名:文字列】を〈【アイコン色:色情報】で〉アイコン取得する手順:画像
		【色:色情報】
		もしアイコン色が無でないなら色=アイコン色
		そうでなければ色=テーマ文字色
		
		【フィルター:RGBフィルタ】というRGBフィルタを作る
			その赤=色の赤
			その緑=色の緑
			その青=色の青
		
		[「[アイコン名].png」を素材リストから画像として取得したもの]にフィルターを適用したものを返す
	終わり
	
	【部品】から子部品取得する手順:配列
		エラーを無視する
		【子部品】
		【子子部品】
		【一覧】=空の配列
		
		もし部品が分割パネルであるなら
			部品の右側の子部品一覧を子部品へそれぞれ繰り返す
				子部品から子部品取得したものを子子部品へそれぞれ繰り返す
					一覧へ子子部品を追加する
				繰り返し終わり
			繰り返し終わり
			部品の左側の子部品一覧を子部品へそれぞれ繰り返す
				子部品から子部品取得したものを子子部品へそれぞれ繰り返す
					一覧へ子子部品を追加する
				繰り返し終わり
			繰り返し終わり
		もし終わり
		部品の子部品一覧を子部品へそれぞれ繰り返す
			子部品から子部品取得したものを子子部品へそれぞれ繰り返す
				一覧へ子子部品を追加する
			繰り返し終わり
		繰り返し終わり
		一覧へ部品を追加する
		
		一覧を返す
	終わり
	
	【画面:ウィンドウ】を〈【例外部品一覧:配列】で〉デザイン適用手順
		もし画面が無なら
			手順から抜け出す
		もし終わり
		
		デザイン適用処理中=○
		
		もし例外部品一覧が無なら例外部品一覧=空の配列
		//もし[メイン設定について「Design」から「Theme.ButtonSpecificColor」を取得したもの]が×なら例外部品一覧={}
		
		/*もし画面が「メイン画面」でないなら
			画面の透明度=0
			繰り返す
				もし画面の透明度が0でなければ
					画面の透明度を([画面の透明度]-10)に変える
					0.0001秒待つ
				そうでなければ
					繰り返しを抜け出す
				もし終わり
			繰り返し終わり
		もし終わり*/
		
		//「　部品一覧: [部品一覧]」をデバッグログへ出力する
		
		【例外部品辞書:辞書】=空の辞書
		例外部品一覧を【例外部品】へそれぞれ繰り返す
			もし例外部品が「;」で終わるなら例外部品辞書へ[例外部品を「;」で区切ったもの(1)]として[例外部品を「;」で区切ったもの(2)]を設定する
			そうでなければ例外部品辞書へ[例外部品]として「」を設定する
		繰り返し終わり
		//「例外部品辞書 - [画面]: [例外部品辞書]」をデバッグログへ出力する
		
		例外監視
		もし画面の内容がアプリの名前を含まないなら
			画面の内容=画面の内容&「 - 」&アプリの名前
		もし終わり
		
		【部品名:文字列】
		【部品フォント:フォント】
		部品一覧(「[画面]」)を【部品】へそれぞれ繰り返す
			もし部品がウィンドウであるなら部品名=「[部品]」
			そうでなければ部品名=部品の名前
			
			//「- [部品名]」をデバッグログへ出力する
			
			// 例外部品一覧に1つも部品が指定されていない場合の処理
			もし例外部品辞書の個数が0なら
				部品の文字色をテーマ文字色の要素に変える
				部品の背景色をテーマ背景色の要素に変える
				繰り返しを続ける
			もし終わり
			
			// 例外部品に指定されている部品の処理
			もし例外部品辞書の見出しから[部品名]を完全一致で探したものの個数が1以上なら
				//「-- 例外部品指定あり - 対象: [例外部品辞書(部品名)]」をデバッグログへ出力する
				例外部品辞書(部品名)について分岐
				「背景色」の場合 // 背景色が例外として指定されている場合、文字色だけ変える
					もしテーマ部品固有色が「×」なら部品の文字色をテーマ文字色の要素に変える
					
				「全て」の場合 // 背景色と文字色の両方が指定されている場合、どちらも変えない
					
					
				その他の場合 // それ以外(何も指定されていない)の場合
					もしテーマ部品固有色が「×」なら部品の文字色をテーマ文字色の要素に変える
					部品の背景色をテーマ背景色の要素に変える
					
				分岐終わり
				
			// 例外部品に指定されていない部品の処理
			そうでなければ
				//「-- 例外部品指定なし」をデバッグログへ出力する
				部品の文字色をテーマ文字色の要素に変える
				部品の背景色をテーマ背景色の要素に変える
				
			もし終わり
			
			// フォントを適用
			例外監視
				/*部品フォント=部品のフォント
				部品フォントの名前=メイン設定について「Design」から「Theme.Font.Name」を取得したもの
				部品フォントのサイズを[メイン設定について「Design」から「Theme.Font.Size」を取得したもの]だけ増やす
				部品のフォント=部品フォント*/
			発生した場合
				/*メイン設定について「Design」で「Theme.Font.Name」へ「Yu Gothic UI」を設定する
				メイン設定について「Design」で「Theme.Font.Size」へ「0」を設定する
				部品フォント=部品のフォント
				部品フォントの名前=「Yu Gothic UI」*/
			監視終わり
		繰り返し終わり
		発生した場合
			//「デザイン適用エラー: [エラーのメッセージ]」をデバッグログへ出力する
		監視終わり
		
		例外監視
			もし[画面のアイコン一覧の個数]が1以上なら
				【テーマアイコン色】=テーマ文字色
				【仮テーマアイコン色】
				【背景】=「」
				//デバッグログへ「　アイコン一覧: [画面のアイコン一覧]」を出力する
				画面のアイコン一覧の見出しをアイコン部品へそれぞれ繰り返す
					//デバッグログへ「　アイコンデザイン適用: [アイコン部品]」を出力する
					「部品=画面の[アイコン部品]」を命令する
					// 部品がコンテキストメニューの項目の場合、背景が白なのでアイコンの色を黒にする
					もし[画面のアイコン一覧([アイコン部品])(2)]が無でないならテーマアイコン色=画面のアイコン一覧([アイコン部品])(2)
					そうでなければテーマアイコン色=テーマ文字色
					
					もし部品がメニュー項目であるなら
						仮テーマアイコン色=テーマアイコン色
						テーマアイコン色=RGB(255,255,255)
					もし終わり
					
					もし[画面のアイコン一覧([アイコン部品])(2)]が無でないならテーマアイコン色=[画面のアイコン一覧([アイコン部品])(2)]
					もし[画面のアイコン一覧([アイコン部品])(3)]が○なら
						部品の背景画像=[ウィンドウ管理器:「[画面のアイコン一覧([アイコン部品])(1)]」をテーマアイコン色でアイコン取得したもの]
						部品の背景画像配置=「拡大」
						
					そうでなければ
						部品の画像=[ウィンドウ管理器:「[画面のアイコン一覧([アイコン部品])(1)]」をテーマアイコン色でアイコン取得したもの]
						
					もし終わり
					
					// アイコン色を戻す
					もし部品がメニュー項目であるなら
						テーマアイコン色=仮テーマアイコン色
					もし終わり
				繰り返し終わり
			もし終わり
		発生した場合
			「アイコン適用エラー: [エラーのメッセージ]」をデバッグログへ出力する
		監視終わり
		
		デザイン適用処理中=×
	終わり
	
	-初期化状態:辞書
	自分で【部品】を初期化開始する手順
		もし初期化状態に「[部品のハンドル]」が存在しないなら初期化状態(「[部品のハンドル]」)=×
		「初期化開始: [部品] (名前: [部品の名前] | ハンドル: [部品のハンドル])」をデバッグログへ出力する
		もし初期化状態(「[部品のハンドル]」)が○でないなら
			「- 実行」をデバッグログへ出力する
			初期化状態(「[部品のハンドル]」)=○
			部品をウィンドウ部品制御で初期化開始する
		もし終わり
	終わり
	
	自分で【部品】を初期化終了する手順
		もし初期化状態に「[部品のハンドル]」が存在しないなら初期化状態(「[部品のハンドル]」)=×
		「初期化終了: [部品] (名前: [部品の名前] | ハンドル: [部品のハンドル])」をデバッグログへ出力する
		「- 実行」をデバッグログへ出力する
		初期化状態(「[部品のハンドル]」)=×
		部品をウィンドウ部品制御で初期化終了する
	終わり
終わり
