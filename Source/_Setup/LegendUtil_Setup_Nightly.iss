; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "LegendUtil Nightly Build"
#define MyAppVersion "0"
#define MyAppReleaseChannel "nightly"
#define MyAppReleaseNumber "nightly"
#define MyAppVersionText "nightly"
#define MyAppPublisher "femto Community Software Team"
#define MyAppCopyrighter "Milkeyyy"
#define MyAppURL "https://github.com/femtoCommunity/LegendUtil"
#define MyAppSupportURL "https://discord.gg/bMf9dDjndC"
#define MyAppExeName "LegendUtil.exe"
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4C1362D7-D28A-49B7-A42E-05BE8B8D0E0A}
AppName={#MyAppName}
AppVersion={#MyAppVersionText}
VersionInfoVersion={#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppCopyright=Copyright (C) 2024 {#MyAppCopyrighter}
AppSupportURL={#MyAppSupportURL}
AppUpdatesURL={#MyAppURL}
DefaultDialogFontName=Yu Gothic UI
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
ChangesAssociations=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=..\..\_Pack\LegendUtil\License.txt
DisableWelcomePage=no
WizardImageStretch=yes
WizardImageFile=..\..\Resources\Logo\LegendUtil_Setup_Banner_White.bmp
WizardSmallImageFile=..\..\Resources\Logo\LegendUtil_Icon_128x128.bmp
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=..\..\_Pack
OutputBaseFilename=LegendUtil_Setup
SetupIconFile=..\..\Resources\Logo\LegendUtil_Icon.ico
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
; Installer and Application Architecture
ArchitecturesInstallIn64BitMode=x64compatible
ArchitecturesAllowed=x64compatible

[Languages]
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\..\Resources\7-Zip\7za.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
;Source: "..\..\_Pack\LegendUtil\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
;Source: "..\..\_Pack\LegendUtil\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[InstallDelete]
Type: files; Name: "{app}\LegendUtil.CursorLocker.dll"
Type: files; Name: "{app}\LegendUtil.DisplayDeviceHelper.dll"
Type: files; Name: "{app}\LockCursorInWindowPlugin.dll"
Type: files; Name: "{app}\Milkeyyy.ControlSR.dll"
Type: files; Name: "{app}\ResolutionChangerPlugin.dll"
Type: files; Name: "{app}\Transitions.dll"
Type: files; Name: "{app}\Transitions.rdr.dll"
Type: filesandordirs; Name: "{app}\runtimes\win-arm64"
Type: filesandordirs; Name: "{app}\runtimes\win-x86"

[Registry]
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: {tmp}\7za.exe; Parameters: "x ""{tmp}\LegendUtil_nightly_latest.zip"" -o""{app}\"" * -r -aoa"; Flags: runhidden runascurrentuser;
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\{#MyAppExeName}"; Parameters: "/AfterUpdate {#MyAppVersion}" ; Flags: nowait skipifnotsilent

[Messages]
BeveledLabel={#MyAppName} Setup [App Version: {#MyAppVersionText}]

[Code]
var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
  DownloadPage.ShowBaseNameInsteadOfUrl := True;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    // Use AddEx to specify a username and password
    DownloadPage.Add('https://github.com/femtoCommunity/LegendUtil/raw/releases/nightly/Pack/LegendUtil_nightly_latest.zip', 'LegendUtil_nightly_latest.zip', '');
    DownloadPage.Show;
    try
      try
        DownloadPage.Download; // This downloads the files to {tmp}
        Result := True;
      except
        if DownloadPage.AbortedByUser then
          Log('Aborted by user.')
        else
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;