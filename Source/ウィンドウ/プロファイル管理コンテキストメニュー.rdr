プロファイル管理コンテキストメニューとは
	ウィンドウを継承する
	
	+例外部品一覧={}
	+アイコン一覧:辞書={
		「アプリ内ブラウザーボタン」=「sharp_language」,
		「検索エンジン管理ボタン」=「sharp_format_list_bulleted」,
		「設定ボタン」=「sharp_settings」,
		「終了ボタン」=「sharp_close」
	}
	+項目一覧:辞書={
		「プロファイル名ラベル」={「」,「」},
		「セパレーター1」={「」,「」},
		「編集ボタン」={「編集」,「「「編集」を表示する」」},
		「名称変更ボタン」={「名前の変更」,「「「名前の変更」を表示する」」},
		「削除ボタン」={「削除」,「「「削除」を表示する」」}
	}
	+ウィンドウ高さ:整数=0
	+項目高さ:整数=36
	
はじめの手順
	初期化する
	この設計スケール比率を[アプリの設計スケール比率]に変える
	この内容=「[アプリの名前]」
	この透明度を0に変える
	この高さ=0
	
	メインパネルの枠=「線」
	
	【項目名一覧】=項目一覧の見出し
	項目名一覧を逆に並べ替える
	
	「「ラベルをメインパネルへ作る
		その内容を「」に変える
		そのドッキング方向を「上」に変える
		その大きさを｛200,7｝に変える
		その余白を｛0,0,0,0｝に変える
		その文字配置を「左」に変える
		そのフォントを「Yu Gothic UI,9,太字」に変える」」を命令する
	
	項目名一覧を【項目名】へそれぞれ繰り返す
		もし項目名が「スペーサー」で始まるなら
			「スペーサー[今日のミリ秒]」&「「というラベルをメインパネルへ作る
				そのドッキング方向を「上」に変える
				その大きさを｛200,50｝に変える
				そのフォントを「Yu Gothic UI,9」に変える」」を命令する
			
		他でもし項目名が「セパレーター」で始まるなら
			「「ラベルをメインパネルへ作る
				その内容を「」に変える
				そのドッキング方向を「上」に変える
				その大きさを｛200,7｝に変える
				その余白を｛0,0,0,0｝に変える
				その文字配置を「左」に変える
				そのフォントを「Yu Gothic UI,9,太字」に変える」」を命令する
			
			項目名&「「というパネルをメインパネルへ作る
				そのドッキング方向を「上」に変える
				その大きさを｛200,1｝に変える
				その余白を｛13,7,10,0｝に変える
				その枠を「線」に変える」」を命令する
			
			「「ラベルをメインパネルへ作る
				その内容を「」に変える
				そのドッキング方向を「上」に変える
				その大きさを｛200,7｝に変える
				その余白を｛0,0,0,0｝に変える
				その文字配置を「左」に変える
				そのフォントを「Yu Gothic UI,9,太字」に変える」」を命令する
			
		他でもし項目名が「ラベル」で終わるなら
			項目名&「「というラベルをメインパネルへ作る
				その内容を「」に変える
				そのドッキング方向を「上」に変える
				その大きさを｛200,25｝に変える
				その余白を｛13,7,10,0｝に変える
				その文字配置を「左」に変える
				そのフォントを「Yu Gothic UI,9,太字」に変える」」を命令する
			
		そうでなければ
			項目名&「というボタンをメインパネルへ作る」&「「
				そのクリックされた時の手順は、『」」&項目一覧(項目名)(2)&「「』
				その内容を「[項目一覧(項目名)(1)]」に変える
				その位置と大きさを｛0,0,200,40｝に変える
				その見た目を「フラット」に変える
				その画像配置を「左」に変える
				その文字配置を「左」に変える
				その文字画像配置を「横に画像と文字」に変える
				そのフォーカス移動可能を×に変える
				そのフォントを「Yu Gothic UI,9」に変える
				その余白を｛10,0,10,0｝に変える
				そのドッキング方向を「上」に変える」」を命令する
			
		もし終わり
	繰り返し終わり
		
	このメインパネルの子部品一覧を【子部品】へそれぞれ繰り返す
		もし子部品がボタンであるなら
			// 項目(ボタン)の高さを設定
			子部品の高さ=項目高さ
			// ボタンの縁取りを消す
			もし子部品の見た目が「フラット」なら子部品の元実体の「FlatAppearance」の「BorderSize」=0
			// 見た目をいい感じにするために先頭に全角スペースを追加する
			もし子部品の内容が「　」で始まらないなら子部品の内容=「　」&子部品の内容
		もし終わり
		ウィンドウ高さ=ウィンドウ高さ+子部品の高さ
	繰り返し終わり
	
	メインパネルの高さ=ウィンドウ高さ
	この大きさ={180,ウィンドウ高さ}
	ーー貼り付けた部品に対する操作をここに書きます
終わり
	初期化する手順
		ーー自動生成された手順です。ここにプログラムを書き加えても消える場合があります
		初期化開始する
		このタスクバー表示を×に変える
		この実質大きさを｛203,251｝に変える
		この最前面を○に変える
		この種類を「縁無し」に変える
		この最大化ボタンを×に変える
		この最小化ボタンを×に変える
		このコントロールボックスを×に変える
		この初期位置を「手動」に変える
		この内容を「」に変える
		この自動調整を○に変える
		このフォントを「Yu Gothic UI,9」に変える
		メインパネルというパネルを作る
			その位置と大きさを｛0,0,200,100｝に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「全体」に変える
		初期化終了する
	終わり
	開いた時の手順
		ウィンドウ管理器の部品一覧へ「[自分]」として[ウィンドウ管理器:自分から子部品取得したもの]を設定する
		ウィンドウ管理器:自分を例外部品一覧でデザイン適用する
		自分の透明度=100
		自分にフォーカスする
	終わり
	
	非アクティブ時の手順
		自分を隠す
		自分の透明度=0
	終わり
	
	自分で項目有効化する手順
		プロファイル管理コンテキストメニューのメインパネルの子部品一覧を【子部品】へそれぞれ繰り返す
			子部品の有効=○
		繰り返し終わり
		//プロファイル名前項目の有効=×
	終わり
	
	自分で項目無効化する手順
		プロファイル管理コンテキストメニューのメインパネルの子部品一覧を【子部品】へそれぞれ繰り返す
			子部品の有効=×
		繰り返し終わり
		//プロファイル名前項目の内容=「未選択」
		//プロファイル作成項目の有効=○
	終わり
終わり