アプリ設定パネル部品とは
	カスタムウィンドウ部品を継承する
	
	+タイトル:文字列=「アプリケーション設定」
	
	+コンフィグ:設定ファイル
	+値部品一覧:辞書=空の辞書
	
	はじめの手順
		初期化する
		このドッキング方向=「全体」
		
		読み込み中パネルのドッキング方向=「全体」
		読み込み中パネルを最前面
		
		ツールバーパネルを最背面
		
		保存ボタンの背景画像=[ウィンドウ管理器:「save_FILL1_wght400_GRAD0_opsz48」を[ウィンドウ管理器のテーマアイコン色]でアイコン取得したもの]
		保存ボタンの元実体の「FlatAppearance」の「BorderSize」=0
		ーー貼り付けた部品に対する操作をここに書きます
	終わり
	初期化する手順
		ーー自動生成された手順です。ここにプログラムを書き加えても消える場合があります
		この位置と大きさを｛15,15,340,183｝に変える
		この内容を「アプリ設定パネル部品」に変える
		このフォントを「Yu Gothic UI,9」に変える
		この余白を｛5｝に変える
		メインパネルというパネルを作る
			その位置と大きさを｛5,35,330,143｝に変える
			そのスクロールバー表示を○に変える
			その移動順を2に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「全体」に変える
		読み込み中パネルというパネルを作る
			その位置と大きさを｛139,82,200,100｝に変える
			その移動順を1に変える
			そのフォントを「Yu Gothic UI,9」に変える
			その余白を｛5｝に変える
			ラベル1というラベルを読み込み中パネルへ作る
				その位置と大きさを｛5,5,190,90｝に変える
				その内容を「読み込み中」に変える
				そのフォントを「Yu Gothic UI,9.75,太字」に変える
				その文字配置を「中央」に変える
				そのドッキング方向を「全体」に変える
		ツールバーパネルというパネルを作る
			その位置と大きさを｛5,5,330,30｝に変える
			その移動順を3に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「上」に変える
			ラベル2というラベルをツールバーパネルへ作る
				その位置と大きさを｛36,0,294,30｝に変える
				その内容を「変更内容は閉じる際に自動保存されます」に変える
				そのフォントを「Yu Gothic UI,9,太字」に変える
				その語尾省略を○に変える
				その文字配置を「右」に変える
				その移動順を1に変える
				そのドッキング方向を「右」に変える
			保存ボタンというボタンをツールバーパネルへ作る
				その位置と大きさを｛0,0,30,30｝に変える
				その見た目を「フラット」に変える
				その移動順を2に変える
				そのフォントを「Yu Gothic UI,9」に変える
				その背景画像配置を「拡大」に変える
				そのドッキング方向を「左」に変える
	終わり
	
	自分を隠す手順
		コンフィグ保存する
		自分の表示=×
	終わり
	
	自分へ【対象コンフィグ:設定ファイル】を適用する手順
		コンフィグ=対象コンフィグ
		
　		【セクション一覧】=コンフィグ管理器のコンフィグ初期値の見出し
		【見出し一覧】
		【見出し名】
		【初期値辞書】
		【初期値】
		
		【セクション部品】
		【ベース部品】
		【値部品】
		【追加項目部品】
		
		【追加項目命令】
		
		【例外セクション一覧】={「Info」,「Window_Main」}
		
		【検索結果:配列】
		
		読み込み中パネルを表示する
		休憩する
		メイン画面を初期化開始する
		メインパネルをクリアする
		
		値部品一覧=空の辞書
		初期値辞書=コンフィグ管理器のコンフィグ初期値
		
		セクション一覧を【セクション名】へそれぞれ繰り返す
			もし例外セクション一覧からセクション名を完全一致で探したものの個数が1以上なら繰り返しを続ける
			
			// セクションのベース部品を生成
			セクション部品=アプリ設定セクション項目部品を自分のメインパネルへ作る
				そのセクション名ラベルの内容=[セクション名]
				その高さ=0
				そのメインパネルの高さ=0
			セクション部品を最前面
			
			見出し一覧=対象コンフィグについてセクション名から見出しを列挙したもの
			見出し一覧を見出し名へそれぞれ繰り返す
				// 初期値辞書に存在しない項目は生成しない
				もし初期値辞書にセクション名が存在しないなら
					繰り返しを続ける
				そうでなければ
					もし初期値辞書(セクション名)に見出し名が存在しないなら
						繰り返しを続ける
					もし終わり
				もし終わり
				初期値=初期値辞書(セクション名)(見出し名)
				//初期値をデバッグログへ出力する
				
				// 項目のベース部品を生成
				ベース部品=アプリ設定項目部品をセクション部品のメインパネルへ作ったもの
				ベース部品を最前面
				
				もし初期値(「型」)が整数なら
					//値部品=アプリ設定ダイヤル部品をベース部品の項目パネルへ作ったもの
					値部品=アップダウンダイアルをベース部品の項目パネルへ作ったもの
						その位置と大きさを｛0,12,150,23｝に変える
						その移動順を1に変える
						そのフォントを「Yu Gothic UI,9」に変える
						その位置固定方向を「左+右」に変える
						そのドッキング方向を「上」に変える
						//その変更された時の手順は、『「メイン設定について[「][セクション名][」]で[「][見出し名][」]へ値部品の値を設定する」を命令する』
					//値部品の縦=(ベース部品の項目パネルの高さ/2)-(値部品の高さ/2)
					
					もし(初期値に「最小値」が存在する)かつ(初期値に「最大値」が存在する)なら
						値部品の最小値=[初期値(「最小値」)]
						値部品の最大値=[初期値(「最大値」)]
						
					そうでなければ
						値部品の最小値=1
						値部品の最大値=99999
						
					もし終わり
					値部品の値=[対象コンフィグについて[セクション名]から[見出し名]を[コンフィグ管理器のコンフィグ初期値(セクション名)(見出し名)(「初期値」)]で取得したもの]
					
				他でもし初期値(「型」)が文字列なら
					もし初期値に「一覧」が存在するなら
						//値部品=アプリ設定選択リスト部品をベース部品の項目パネルへ作ったもの
						値部品=選択リストをベース部品の項目パネルへ作ったもの
							その位置と大きさを｛0,12,150,23｝に変える
							その入力可能を×に変える
							その選択窓幅を150に変える
							その項目高さを15に変える
							その移動順を1に変える
							そのフォントを「Yu Gothic UI,9」に変える
							その位置固定方向を「左+右」に変える
							そのドッキング方向を「上」に変える
							//その選択された時の手順は、『「メイン設定について[「][セクション名][」]で[「][見出し名][」]へ値部品の選択項目を設定する」を命令する』
						//値部品の縦=(ベース部品の項目パネルの高さ/2)-(値部品の高さ/2)
						
							値部品の一覧=[初期値(「一覧」)]
							検索結果=値部品の一覧から[対象コンフィグについて[セクション名]から[見出し名]を[コンフィグ管理器のコンフィグ初期値(セクション名)(見出し名)(「初期値」)]で取得したもの]を完全一致で探したもの
							もし検索結果の個数が1以上なら
								値部品の選択番号を[検索結果(1)]に変える
								
							そうでなければ
								値部品の選択番号を1に変える
								
							もし終わり
						
					そうでなければ
						//値部品=アプリ設定テキスト部品をベース部品の項目パネルへ作ったもの
						値部品=テキストをベース部品の項目パネルへ作ったもの
							その位置と大きさを｛0,12,150,23｝に変える
							その行間を16に変える
							その移動順を1に変える
							そのフォントを「Yu Gothic UI,9」に変える
							その位置固定方向を「左+右」に変える
							そのドッキング方向を「上」に変える
							//その内容が変化した時の手順は、『「メイン設定について[「][セクション名][」]で[「][見出し名][」]へ値部品の内容を設定する」を命令する』
						//値部品の縦=(ベース部品の項目パネルの高さ/2)-(値部品の高さ/2)
						値部品の内容=[対象コンフィグについて[セクション名]から[見出し名]を[コンフィグ管理器のコンフィグ初期値(セクション名)(見出し名)(「初期値」)]で取得したもの]
						
					もし終わり
					
				他でもし初期値(「型」)が真偽値なら
					//値部品=アプリ設定チェックボックス部品をベース部品の項目パネルへ作ったもの
					値部品=チェックボックスをベース部品の項目パネルへ作ったもの
						その位置と大きさを｛0,12,150,23｝に変える
						そのチェックボックス位置を「右」に変える
						その移動順を1に変える
						そのフォントを「Yu Gothic UI,9」に変える
						その位置固定方向を「左+右」に変える
						そのドッキング方向を「上」に変える
						//そのチェックされた時の手順は、『「メイン設定について[「][セクション名][」]で[「][見出し名][」]へ値部品のチェックを設定する」を命令する』
					//値部品の縦=(ベース部品の項目パネルの高さ/2)-(ベース部品の値チェックボックスの高さ/2)
					値部品のチェック=[対象コンフィグについて[セクション名]から[見出し名]を[コンフィグ管理器のコンフィグ初期値(セクション名)(見出し名)(「初期値」)]で取得したもの]
					
				もし終わり
				値部品の名前=「値部品」
				値部品一覧へ「[セクション名]/[見出し名]」として値部品を設定する
				
				ベース部品のセクション名=[セクション名]
				ベース部品のキー名=[見出し名]
				「セクション: [ベース部品のセクション名]/キー: [ベース部品のキー名]」をデバッグログへ出力する
				ベース部品の見出しラベルの内容=[初期値(「名前」)]
				ベース部品の説明ラベルの内容=[初期値(「説明」)]
				ベース部品の高さ=ベース部品の見出しラベルの高さ+ベース部品の説明ラベルの高さ+ベース部品のセパレーターラベルの高さ+ベース部品のラベルパネルの余白の下+ベース部品の項目パネルの余白の上+ベース部品の余白の上+ベース部品の余白の下
				
				// コンフィグの見出しに対応する値部品のテーブル(辞書)を作る
				// ここにキーをコンフィグの見出しで値が値部品として辞書に加えるコードを書く
				
				// 追加項目を生成する
				もし初期値に「追加項目」が存在するなら
					初期値(「追加項目」)を追加項目命令へそれぞれ繰り返す
						「[初期値(「名前」)]」をデバッグログへ出力する
						ラベルをベース部品の項目パネルへ作る
							その高さ=3
							そのドッキング方向=「上」
							それを最前面
						追加項目部品=[追加項目命令の内容]を命令する
						追加項目部品のドッキング方向=「上」
						ベース部品の高さ=ベース部品の高さ+追加項目部品の高さ
						追加項目部品を最前面
					繰り返し終わり
				もし終わり
				
				セクション部品の高さ=セクション部品の高さ+ベース部品の高さ
			繰り返し終わり
			セクション部品の高さ=セクション部品のセクション名ラベルの高さ+セクション部品のスペーサーパネルの高さ+セクション部品のメインパネルの高さ
			セクション部品を最前面
		繰り返し終わり
		メイン画面を初期化終了する
		
		// ウィンドウデザイン適用
		ウィンドウ管理器の部品一覧へ「[メイン画面]」として[ウィンドウ管理器:メイン画面から子部品取得したもの]を設定する
		ウィンドウ管理器:メイン画面をメイン画面の例外部品一覧でデザイン適用する
		
		読み込み中パネルを隠す
	終わり
	
	自分でコンフィグ読み込む手順
		値部品一覧の見出しを【見出し】へそれぞれ繰り返す
			セクション=見出しを「/」で区切ったもの(1)
			キー=見出しを「/」で区切ったもの(2)
			部品=値部品一覧(見出し)
			もし部品がテキストであるかつ部品の名前が「値部品」なら
				部品の内容=コンフィグについて[セクション]から[キー]を取得したもの
				
			他でもし部品が選択リストであるかつ部品の名前が「値部品」なら
				// 選択リストの選択番号を該当する項目に当たる番号にする 見つからなければ1にする
				部品の一覧=[コンフィグ管理器のコンフィグ初期値(セクション)(キー)(「一覧」)]
				【検索結果】=部品の一覧から[コンフィグについて[セクション]から[キー]を無で取得したもの]を完全一致で探したもの
				「一覧: [部品の一覧] / 検索結果: [検索結果]」をデバッグログへ出力する
				もし検索結果の個数が1以上なら
					部品の選択番号=[検索結果(1)]
					
				そうでなければ
					部品の選択番号=1
					
				もし終わり
				
			他でもし部品がアップダウンダイアルであるかつ部品の名前が「値部品」なら
				部品の値=コンフィグについて[セクション]から[キー]を取得したもの
				
			他でもし部品がチェックボックスであるかつ部品の名前が「値部品」なら
				部品のチェック=コンフィグについて[セクション]から[キー]を取得したもの
				
			もし終わり
		繰り返し終わり
	終わり
	
	自分でコンフィグ保存する手順
		「アプリ設定保存実行」をデバッグログへ出力する
		【セクション】
		【キー】
		【部品】
		値部品一覧の見出しを【見出し】へそれぞれ繰り返す
			セクション=見出しを「/」で区切ったもの(1)
			キー=見出しを「/」で区切ったもの(2)
			部品=値部品一覧(見出し)
			「部品: [部品] / 名前: [部品の名前] / 種類: [部品の種類名]」をデバッグログへ出力する
			もし部品がテキストであるかつ部品の名前が「値部品」なら
				コンフィグについて[セクション]で[キー]へ[部品の内容]を設定する
				
			他でもし部品が選択リストであるかつ部品の名前が「値部品」なら
				コンフィグについて[セクション]で[キー]へ[部品の選択項目]を設定する
				
			他でもし部品がアップダウンダイアルであるかつ部品の名前が「値部品」なら
				コンフィグについて[セクション]で[キー]へ[部品の値]を設定する
				
			他でもし部品がチェックボックスであるかつ部品の名前が「値部品」なら
				コンフィグについて[セクション]で[キー]へ[部品のチェック]を設定する
				
			もし終わり
		繰り返し終わり
		
		/*
		//例外監視
		[ウィンドウ管理器:自分のメインパネルから子部品取得したもの]を【子部品】へそれぞれ繰り返す
			「[子部品]([子部品の名前]) 種類名: [子部品の種類名]」をデバッグログへ出力する
			「- [子部品の親の親]([子部品の親の親の名前]) 種類名: [子部品の親の親の種類名]」をデバッグログへ出力する
			「 - [子部品の親の親のセクション名]/[子部品の親の親のキー名]: [子部品の内容]」をデバッグログへ出力する
			もし子部品がテキストであるかつ子部品の名前が「値部品」なら
				メイン設定について[子部品の親の親のセクション名]で[子部品の親の親のキー名]へ[子部品の内容]を設定する
				
			他でもし子部品が選択リストであるかつ子部品の名前が「値部品」なら
				メイン設定について[子部品の親の親のセクション名]で[子部品の親の親のキー名]へ[子部品の選択項目]を設定する
				
			他でもし子部品がアップダウンダイアルであるかつ子部品の名前が「値部品」なら
				メイン設定について[子部品の親の親のセクション名]で[子部品の親の親のキー名]へ[子部品の値]を設定する
				
			他でもし子部品がチェックボックスであるかつ子部品の名前が「値部品」なら
				メイン設定について[子部品の親の親のセクション名]で[子部品の親の親のキー名]へ[子部品のチェック]を設定する
				
			もし終わり
		繰り返し終わり
		
		// アプリセクション項目部品 (セクション部品)
		自分のメインパネルの子部品一覧を【子部品1】へそれぞれ繰り返す
			「[子部品1]([子部品1の名前]) 種類名: [子部品1の種類名]」をデバッグログへ出力する
			もし子部品1がカスタムウィンドウ部品であるなら
				// メインパネル
				もし子部品の子部品一覧が無なら繰り返しを続ける
				子部品の子部品一覧を【子部品2】へそれぞれ繰り返す
					もし子部品2の名前が「メインパネル」なら
						// アプリ設定項目部品 (ベース部品)
						もし子部品2の子部品一覧が無なら繰り返しを続ける
						子部品2の子部品一覧を【子部品3】へそれぞれ繰り返す
							もし子部品3がカスタムウィンドウ部品であるなら
								// 項目パネル
								もし子部品3の子部品一覧が無なら繰り返しを続ける
								子部品3の子部品一覧を【子部品4】へそれぞれ繰り返す
									もし子部品4の名前が「項目パネル」なら
										もし子部品4の子部品一覧が無なら繰り返しを続ける
										子部品4の子部品一覧を【子部品5】へそれぞれ繰り返す
											もし子部品5の名前が「値部品」なら
												「アプリ設定保存: [子部品1のセクション名ラベルの内容]/[子部品3のキー名]=[子部品5の内容]」をデバッグログへ出力する
												もし子子部品の値部品がテキストであるなら
													メイン設定について[子部品1のセクション名ラベルの内容]で[子部品3のキー名]へ[子部品5の内容]を設定する
													
												他でもし子子部品の値部品が選択リストであるなら
													メイン設定について[子部品1のセクション名ラベルの内容]で[子部品3のキー名]へ[子部品5の選択項目]を設定する
													
												他でもし子子部品の値部品がアップダウンダイアルであるなら
													メイン設定について[子部品1のセクション名ラベルの内容]で[子部品3のキー名]へ[子部品5の値]を設定する
													
												他でもし子子部品の値部品がチェックボックスであるなら
													メイン設定について[子部品1のセクション名ラベルの内容]で[子部品3のキー名]へ[子部品5のチェック]を設定する
													
												もし終わり
											もし終わり
										繰り返し終わり
									もし終わり
								繰り返し終わり
							もし終わり
						繰り返し終わり
					もし終わり
					「[子部品2]([子部品2の名前]) 種類名: [子部品2の種類名]」をデバッグログへ出力する
				繰り返し終わり
			もし終わり
		繰り返し終わり*/
		//発生した場合
		//	「アプリ設定の保存に失敗しました。[改行][改行][エラーのメッセージ]」を「中止アイコン」で表示する
		//監視終わり
	終わり
	
	保存ボタンがクリックされた時の手順
		コンフィグ保存する
	終わり
終わり

アプリ設定セクション項目部品とは
	カスタムウィンドウ部品を継承する
	はじめの手順
		初期化する
		このドッキング方向=「上」
		ーー貼り付けた部品に対する操作をここに書きます
	終わり
	初期化する手順
		ーー自動生成された手順です。ここにプログラムを書き加えても消える場合があります
		この位置と大きさを｛15,15,329,205｝に変える
		この内容を「アプリ設定セクション項目部品」に変える
		このフォントを「Yu Gothic UI,9」に変える
		ベースパネルというパネルを作る
			その位置と大きさを｛0,0,329,205｝に変える
			その枠を「線」に変える
			その移動順を5に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「全体」に変える
			メインパネルというパネルをベースパネルへ作る
				その位置と大きさを｛0,40,327,10｝に変える
				その移動順を4に変える
				その自動調整を○に変える
				そのフォントを「Yu Gothic UI,9」に変える
				そのドッキング方向を「上」に変える
				その余白を｛5｝に変える
			セクション名ラベルというラベルをベースパネルへ作る
				その位置と大きさを｛0,15,57,25｝に変える
				その内容を「ラベル1」に変える
				そのフォントを「Yu Gothic UI,11.25,太字」に変える
				その語尾省略を○に変える
				その自動調整を○に変える
				その文字配置を「左」に変える
				その移動順を1に変える
				そのドッキング方向を「上」に変える
				その余白を｛5,0,0,5｝に変える
			スペーサーパネルというパネルをベースパネルへ作る
				その位置と大きさを｛0,0,327,15｝に変える
				その移動順を3に変える
				そのフォントを「Yu Gothic UI,9」に変える
				そのドッキング方向を「上」に変える
	終わり
終わり

アプリ設定項目部品とは
	カスタムウィンドウ部品を継承する
	
	+セクション名
	+キー名
	
	はじめの手順
		初期化する
		このドッキング方向=「上」
		ーー貼り付けた部品に対する操作をここに書きます
	終わり
	初期化する手順
		ーー自動生成された手順です。ここにプログラムを書き加えても消える場合があります
		この位置と大きさを｛15,15,400,60｝に変える
		この内容を「アプリ設定ダイヤル項目部品」に変える
		このフォントを「Yu Gothic UI,9」に変える
		この余白を｛5｝に変える
		ラベルパネルというパネルを作る
			その位置と大きさを｛5,6,200,49｝に変える
			その移動順を3に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「全体」に変える
			その余白を｛0,5,0,0｝に変える
			説明ラベルというラベルをラベルパネルへ作る
				その位置と大きさを｛0,22,27,15｝に変える
				その内容を「DEF」に変える
				そのフォントを「Yu Gothic UI,9」に変える
				その語尾省略を○に変える
				その自動調整を○に変える
				その移動順を1に変える
				そのドッキング方向を「上」に変える
			見出しラベルというラベルをラベルパネルへ作る
				その位置と大きさを｛0,5,33,17｝に変える
				その内容を「ABC」に変える
				そのフォントを「Yu Gothic UI,9.75,太字」に変える
				その語尾省略を○に変える
				その自動調整を○に変える
				その画像配置を「右下」に変える
				その文字配置を「左下」に変える
				その移動順を2に変える
				そのドッキング方向を「上」に変える
		項目パネルというパネルを作る
			その位置と大きさを｛205,6,190,49｝に変える
			その移動順を3に変える
			そのフォントを「Yu Gothic UI,9」に変える
			そのドッキング方向を「右」に変える
			その余白を｛0,5,0,0｝に変える
		セパレーターラベルというラベルを作る
			その位置と大きさを｛5,5,390,1｝に変える
			そのフォントを「Yu Gothic UI,9」に変える
			その枠を「線」に変える
			その文字配置を「中央」に変える
			その移動順を2に変える
			そのドッキング方向を「上」に変える
	終わり
	
	/*セクション名という属性
		取得する手順
			セクションを返す
		終わり
		設定する手順
			セクション=設定値
		終わり
	終わり
	
	キー名という属性
		取得する手順
			キーを返す
		終わり
		設定する手順
			キー=設定値
		終わり
	終わり*/
終わり

アプリ設定部品ベースとは
	実装なし
	
	セクションを持つ
	キーを持つ
	
	セクション名という属性
		設定する手順
			セクション=設定値
		終わり
		取得する手順
			セクションを返す
		終わり
	終わり
	
	キー名という属性
		設定する手順
			キー=設定値
		終わり
		取得する手順
			キーを返す
		終わり
	終わり

終わり

アプリ設定チェックボックス部品とは
	ウィンドウ部品を継承する
	アプリ設定部品ベースを実装する
	チェックボックスを実装する
終わり

アプリ設定選択リスト部品とは
	ウィンドウ部品を継承する
	アプリ設定部品ベースを実装する
	選択リストを実装する
終わり

アプリ設定テキスト部品とは
	ウィンドウ部品を継承する
	アプリ設定部品ベースを実装する
	テキストを実装する
終わり

アプリ設定ダイヤル部品とは
	ウィンドウ部品を継承する
	アプリ設定部品ベースを実装する
	アップダウンダイアルを実装する
終わり
